#!/usr/bin/perl -w
# 
# surrey-cache-templates
# 
# This script will run daily overnight to fetch and cache various
# council headers and footers to be used for the Surrey petitions website.

# TODO: Add CSS and RSS links in headers.

use strict;
use File::Slurp;
use FindBin;
use LWP::Simple;

use lib "$FindBin::Bin/../perllib";
use lib "$FindBin::Bin/../commonlib/perllib";
 
use mySociety::Config;
BEGIN {
    mySociety::Config::set_file("$FindBin::Bin/../conf/general");
}
use mySociety::DBHandle qw(dbh);
use mySociety::Memcached;
mySociety::Memcached::set_namespace(mySociety::Config::get('PET_DB_NAME'));
use Petitions;

my ($header, $footer);
my $template_root = "$FindBin::Bin/../templates";

# Surrey
$header = get_page('http://www1.surreycc.gov.uk/scctemplate/petitions/header1.asp');
$header .= <<EOF;
<title>Surrey County Council - PARAM_TITLE</title>
<link rel="stylesheet" type="text/css" href="/pet.css" />
<link rel="stylesheet" type="text/css" href="/assets/surreycc/css.css" />
EOF
$header .= get_page('http://www1.surreycc.gov.uk/scctemplate/petitions/header2.asp');
$header =~ s/<link rel="alternate" type="application\/rss+xml".*? \/>/PARAM_RSS_LINKS\nPARAM_SUBJECTS/;
$header =~ s/<link rel="alternate" type="application\/rss+xml".*? \/>//g;
$header =~ s/(<meta name="DC\.Identifier".*?")http.*?"/$1PARAM_DC_IDENTIFIER"/;
$header .= "\n<h1>PARAM_H1</h1>\n";
$footer = get_page('http://www1.surreycc.gov.uk/scctemplate/petitions/footer.asp');
output('surreycc', $header, $footer);

# Surrey Heath
$header = get_page('http://www.surreyheath.gov.uk/general_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - /;
$header =~ s/<\*\*page_title\*\*>/PARAM_TITLE/;
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css"></head>};
$footer = get_page('http://www.surreyheath.gov.uk/general_footer.asp');
output('surreyheath', $header, $footer);

# Elmbridge
$header = get_page('http://www.elmbridge.gov.uk/website_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - /;
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css"></head>};
$footer = get_page('http://www.elmbridge.gov.uk/website_footer.asp');
output('elmbridge', $header, $footer);

# Tandridge
$header = get_page('http://www.tandridge.gov.uk/petitions_header.asp');
$header =~ s/<title>/<title>PARAM_TITLE - Tandridge District Council/;
$header =~ s{</head>}{<link rel="stylesheet" type="text/css" href="/pet.css">
<link rel="stylesheet" type="text/css" href="/assets/tandridge/css.css">
</head>};
$header .= "<div id='mysociety'>\n";
$footer = "</div>\n";
$footer .= get_page('http://www.tandridge.gov.uk/petitions_footer.asp');
output('tandridge', $header, $footer);

sub output {
    my ($dir, $header, $footer) = @_;

    my $flush = 0;

    my $template_dir = "$template_root/$dir";
    mkdir($template_dir);

    if ($header) {
        my $current_header = read_file("$template_dir/head.html");
        if ($current_header ne $header) {
            write_file("$template_dir/head.html", { atomic => 1 }, \$header);
            $flush = 1;
        }
    }
    if ($footer) {
        my $current_footer = read_file("$template_dir/foot.html");
        if ($current_footer ne $footer) {
            write_file("$template_dir/foot.html", { atomic => 1 }, \$footer);
            $flush = 1;
        }
    }

    if ($flush) {
        my $ids = dbh()->selectcol_arrayref('select id from petition where body_id=(select id from body where ref=?)', {}, $dir);
        foreach (@$ids) {
            mySociety::Memcached::set("lastupdate:$_", time());
        }
    }
}

sub get_page {
    my $url = shift;
    my $r = LWP::Simple::get($url);
    return $r || '';
}
